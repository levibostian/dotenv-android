/**
 * Help on how to create and publish a gradle plugin:
 * https://docs.gradle.org/current/userguide/publishing_gradle_plugins.html
 * https://plugins.gradle.org/docs/submit
 */

buildscript {
    ext.kotlin_version = '1.7.21'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    // publishes the gradle plugin
    // this 1 plugin includes `java-gradle-plugin` for authoring the gradle plugin and
    // `maven-publish` which generates the maven plugin metadata.
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.apache.commons:commons-compress:1.21'
    compileOnly('com.android.tools.build:gradle:4.0.2')
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

ext.getPluginVersion = { ->
    def envVersion = System.getenv('PLUGIN_VERSION')
    if (envVersion != "" && envVersion != null) return envVersion
    else return '1.0.0'
}

group 'earth.levi'
version getPluginVersion() // version used when publishing

// configure 'com.gradle.plugin-publish'. from: https://docs.gradle.org/current/userguide/publishing_gradle_plugins.html#configure_the_plugin_publishing_plugin
pluginBundle {
    website = 'https://github.com/levibostian/dotenv-android'
    vcsUrl = 'https://github.com/levibostian/dotenv-android'
    description = 'Android gradle plugin to use .env file in your Android app'
    tags = ['android', 'gradle', 'kotlin', 'dotenv', 'dotenv-android', '.env']
}

// configure 'java-gradle-plugin'. from: https://docs.gradle.org/current/userguide/java_gradle_plugin.html
gradlePlugin {
    plugins {
        dotenvPlugin {
            id = 'earth.levi.dotenv-android'
            displayName = 'dotenv-android'
            implementationClass = 'earth.levi.dotenv.DotEnvPlugin'
        }
    }
}

publishing {
    publications {
        pluginPublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId "dotenv-android"
            version project.version
        }
    }

    repositories {
        // publishToMavenLocal task is already added by the maven publish plugin

        // Allows you to deploy snapshot builds.
        // By default, we use production which points to the gradle plugin repository.
        if (project.version.endsWith("SHAPSHOT")) {
            maven {
                url = "https://maven.pkg.github.com/levibostian/dotenv-android"
            }
        }
    }
}