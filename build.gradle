buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.11.0'
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

repositories {
    jcenter()
    google()
}

dependencies {
    implementation gradleApi()
    implementation 'com.squareup:javapoet:1.10.0'
    implementation 'com.squareup:kotlinpoet:1.6.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compileOnly("com.android.tools.build:gradle:3.6.3")
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

group 'earth.levi'
version '1.0.0'

//Local publishing for development purposes
publishing {
    publications {
        pluginPublication (MavenPublication) {
            from    components.java
            groupId    project.group
            artifactId "dotenv"
            version    project.version
        }
    }
}

pluginBundle {
    website = 'https://github.com/levibostian/dotenv-android'
    vcsUrl = 'https://github.com/levibostian/dotenv-android'
    tags = ['android', 'gradle', 'kotlin', 'dotenv']
}

gradlePlugin {
    plugins {
        dotenvPlugin {
            id = 'earth.levi.dotenv'
            displayName = 'dotenv'
            description = 'Generate source code file from .env values'
            implementationClass = 'earth.levi.dotenv.DotEnvPlugin'
        }
    }
}